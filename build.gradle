plugins {
    id 'java'
    id 'application'
    id 'jacoco'
}

version = '1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

application {
    mainClassName = 'citadels.App' // This ensures correct main entrypoint
}

test {
    useJUnitPlatform()
    ignoreFailures = true
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.destination file("${buildDir}/reports/coverage")
    }
}

jar {
    archiveFileName = "citadels.jar"
    manifest {
        attributes(
            'Main-Class': 'citadels.App'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

javadoc {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
    options.memberLevel = JavadocMemberLevel.PRIVATE
    options.author = true
    options.version = true
    options.windowTitle = 'Citadels Game Documentation'
    options.docTitle = 'Citadels Game API Documentation'
}
